buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"

    }
}

version "4.0.0-SNAPSHOT"
group "org.grails.plugins"
description "The Cookie Session plugin enables grails applications to store session data in http cookies between requests instead of in memory on the server."

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: "org.grails.grails-plugin"
apply plugin: "org.grails.grails-plugin-publish"

ext {
    grailsVersion = project.grailsVersion
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenLocal()
    jcenter()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencies {
    provided 'org.springframework.boot:spring-boot-starter-logging'
    provided "org.springframework.boot:spring-boot-autoconfigure"

    profile "org.grails.profiles:web-plugin"
    provided "org.grails:grails-web-boot"
    provided "org.grails:grails-dependencies"
    provided 'javax.servlet:javax.servlet-api:3.1.0'

    testCompile "org.grails:grails-plugin-testing"

    console "org.grails:grails-console"

    compile group: "com.esotericsoftware", name: "kryo-shaded", version: "4.0.1"
    compile(group: "de.javakaffee", name: "kryo-serializers", version: "0.42") {
        exclude module: 'kryo' // we want kryo-shaded as above
    }
}

bootRepackage.enabled = false

// hack for Gradle 4
jar {
    def expected = new File("${project.sourceSets.main.output.classesDir}/META-INF/grails-plugin.xml")
    def actual = new File(expected.absolutePath.replace('/java/', '/groovy/'))

    outputs.file(expected)
    doLast {
        logger.info "grails-plugin.xml hack, actual = ${actual}, expected = ${expected}"
        if (actual.exists() && !expected.exists()) {
            expected.parentFile.mkdirs()
            expected.text = actual.text
        }
    }
}

grailsPublish {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_APIKEY')
    githubSlug = 'benlucchesi/grails-cookie-session'
    license {
        name = 'Apache-2.0'
    }
    title = "cookie-session"
    desc = project.description
    developers = [benlucchesi:"Ben Lucchesi", double16:"Patrick Double"]
}

task test3_0(type: GradleBuild, group: 'verification') {
    dependsOn publishToMavenLocal
    dir = 'grails3_0'
    tasks = ['test']
}

task integrationTest3_0(type: GradleBuild, group: 'verification') {
    dependsOn publishToMavenLocal
    dir = 'grails3_0'
    tasks = ['integrationTest']
}

task testAll(group: 'verification') {
    dependsOn test
    dependsOn publishToMavenLocal
    //dependsOn test3_0
    dependsOn gradle.includedBuilds*.task(':test')
}

task integrationTestAll(group: 'verification') {
    dependsOn publishToMavenLocal
    //dependsOn integrationTest3_0
    dependsOn gradle.includedBuilds*.task(':integrationTest')
}
